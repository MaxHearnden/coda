dnl Process this file with autoconf to produce a configure script

dnl Initialization
AC_INIT(rpc2-src/rpc2.private.h)
CODA_SETUP_BUILD
AM_INIT_AUTOMAKE(rpc2, 1.2)
AM_CONFIG_HEADER(config.h)

CODA_OPTION_LWP

dnl Checks for programs.
AC_PROG_CC
CODA_PROG_NATIVECC
AC_PROG_CPP
AC_PROG_RANLIB
AC_CHECK_PROG(AR, ar, ar)
export AR
AM_PROG_LEX
AC_PROG_YACC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AM_PROG_LIBTOOL
AC_CHECK_PROG(CP, cp, cp)
AC_CHECK_PROG(PERL, perl, perl)

dnl Checks for libraries.
CODA_FIND_LIB(lwp, [#include <lwp/lwp.h>], [LWP_Init(0,0,0)])
CODA_CHECK_LIBTERMCAP
AC_CHECK_LIB(readline, main, [LIBREADLINE=-lreadline], [], [${LIBTERMCAP}])
AC_SUBST(LIBREADLINE)
AC_SEARCH_LIBS(inet_ntoa, nsl)
AC_SEARCH_LIBS(socket, socket)

AM_CONDITIONAL(HAVE_READLINE, test x${LIBREADLINE} != x)

dnl For playing with the _pt version...
if test ${with_LWP_PT}p = nop ; then
  AC_CHECK_LIB(lwp, LWP_Init, [LIBLWP="-llwp"],
               [AC_MSG_ERROR("Could not locate liblwp")])
else
  AC_CHECK_LIB(pthread, pthread_create, [LIBPTHREAD="-lpthread"])
  AC_CHECK_LIB(lwp_pt, LWP_Init, [LIBLWP="-llwp_pt"],
               [AC_MSG_ERROR("Failed to locate liblwp_pt")], [${LIBPTHREAD}])
fi
AC_SUBST(LIBLWP)
AC_SUBST(LIBPTHREAD)

dnl Checks for header files.
AC_CHECK_HEADERS(sys/stream.h arpa/inet.h arpa/nameser.h)

dnl Checks for typedefs.
dnl Checks for compiler characteristics.

dnl Checks for library functions.
AC_CHECK_FUNCS(inet_aton inet_ntoa ffs iopen)
AC_FUNC_SELECT_ARGTYPES

dnl Checks for system services.
AC_CYGWIN
AC_EXEEXT

if test ${with_LWP_PT}p != nop ; then
  AC_MSG_WARN(["Building against pthreaded lwp libraries, *experimental*"])
fi

dnl Generate output files
AC_OUTPUT(Makefile pkgs/Makefile pkgs/rpc2.spec pkgs/rpc2-dos.spec pkgs/rpc2-nt.spec include/Makefile include/rpc2/Makefile rpc2-src/Makefile rp2gen/Makefile fail/Makefile)

